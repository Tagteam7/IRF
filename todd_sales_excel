   !
   *
   $INCLUDE BP.INC ADV.COMMON
   $INCLUDE BP.INC STANDARD.EQUATES
   *
   EQU TB TO CHAR(9)
   *
   FAILED.OPEN = 0
   CALL OPEN.FILE('CM', FILE.CM, 'M', FAILED.OPEN)
   CALL OPEN.FILE('SA', FILE.SA, 'M', FAILED.OPEN)
   CALL OPEN.FILE('SHS', FILE.SHS, 'M', FAILED.OPEN)
   CALL OPEN.FILE('USER', FILE.USER, 'M', FAILED.OPEN)
   IF FAILED.OPEN THEN CALL GENERIC.REPLY(X); STOP
   *
   OPEN '','REPORTS' TO FILE.RPT ELSE STOP
   *
   $INCLUDE BP.INC CM$
   $INCLUDE BP.INC SH$
   $INCLUDE BP.INC USER$
   *
   DETAIL.REPORT = 0  ;* 0=summary by customer, 1=invoice detail by customer
   *
   LAST.CUSTOMER = AM
   *
   IF NOT(DETAIL.REPORT) THEN
      YEAR.LIST=''
      THIS.YEAR = OCONV(DATE(),'DY')
      START.YEAR = THIS.YEAR - 2
      FOR I=THIS.YEAR TO START.YEAR STEP -1
         YEAR.LIST<-1> = I
      NEXT I
      START.DATE = ICONV('01-01-':START.YEAR,'D')
      CUST.SORT.LIST = ''
      CUSTOMER.LIST = ''
      SALES.LIST=''
   END
   *
   xls=''; row=''; status=''; c=1
   IF DETAIL.REPORT THEN
      xls = 'Order Number' :TB: 'Ship Date' : TB : 'Sales Rep' :TB: 'Rep Name' :TB: 'Customer' :TB: 'Name' :TB
      xls :='City' :TB: 'State' :TB: 'Phone' :TB: 'Invoice Amt'
   END ELSE
      xls = 'Sales Rep' :TB: 'Rep Name' :TB: 'Customer' :TB: 'Name' :TB
      xls :='City' :TB: 'State' :TB: 'Phone'
      LAST.YR = DCOUNT(YEAR.LIST,AM)
      FOR YR.CNTR = 1 TO LAST.YR
         xls := TB : YEAR.LIST<YR.CNTR>:' Sales'
         IF YR.CNTR=1 THEN xls := ' (ytd)'
      NEXT YR.CNTR
   END
   *
   IF DETAIL.REPORT THEN
      execute "get-list et1"
   END ELSE
      CMD = 'SELECT SHS WITH SHIP.DATE GE "':OCONV(START.DATE,'D2-'):'"'
      CMD := ' AND WITH STATUS = "C" AND WITH ORDER.TYPE # "T"'
      CMD := ' BY CUSTOMER BY CUSTOMER.NAME'
      EXECUTE CMD RETURNING INFO
   END
   *
   100 *
   readnext key.shs else
      IF NOT(DETAIL.REPORT) THEN GOSUB CREATE.SUMMARY.ROWS
      go 99000
   END
   read item.shs from file.shs,key.shs else item.shs=''
   *  
   customer = item.shs<sh$customer>
   *
   IF DETAIL.REPORT THEN
      read item.cm from file.cm,customer else
         item.cm='not on customer master file CM'
      end
      *
      sales.rep = item.shs<sh$sales.rep>
      read item.user from file.user,sales.rep else
         item.user='not on user master file USER'
      end
      sales.rep.name=item.user<user$short.name>
      c = c + 1
      row = key.shs :TB
      row :=oconv(item.shs<sh$ship.date>,'d2'):TB
      row :=sales.rep:TB
      row :=sales.rep.name:TB
      row :=customer:TB
      row :=item.cm<cm$name>:TB
      row :=item.cm<cm$city>:TB
      row :=item.cm<cm$state>:TB
      row :=item.cm<cm$phone>:TB
      row :=oconv(item.shs<sh$invoice.amt>,'MR2')
      *
      xls<-1> = row
      *...   if c > 10 then go 99000
      go 100
   END
   *
   *  Summary report
   *
   IF CUSTOMER # LAST.CUSTOMER THEN
      read item.cm from file.cm,customer else
         item.cm='not on customer master file CM'
      end
      *
      sales.rep = item.cm<cm$sales.rep>
      read item.user from file.user,sales.rep else
         item.user='not on user master file USER'
      end
      *
      sales.rep.name=item.user<user$short.name>
      *
      CUST.SORT = SALES.REP 'L#6' : SALES.REP.NAME 'L#20' : ITEM.CM<CM$NAME> 'L#36' : CUSTOMER 'R#10'
      LOCATE CUST.SORT IN CUST.SORT.LIST BY 'AL' SETTING CUST.POS ELSE
         INS CUST.SORT BEFORE CUST.SORT.LIST<CUST.POS>
         INS CUSTOMER BEFORE CUSTOMER.LIST<CUST.POS>
         INS '' BEFORE SALES.LIST<CUST.POS>
      END
      LAST.CUSTOMER = CUSTOMER
   END
   *
   YEAR = OCONV(ITEM.SHS<SH$SHIP.DATE>,'DY')
   LOCATE YEAR IN YEAR.LIST SETTING YEAR.POS ELSE GO 100
   *
   SALES.LIST<CUST.POS,YEAR.POS> += ITEM.SHS<SH$INVOICE.AMT>
   *
   GO 100
   *
   CREATE.SUMMARY.ROWS: *  create the summary row for the customer
   *
   LAST.CUST = DCOUNT(CUSTOMER.LIST,AM)
   FOR I=1 TO LAST.CUST
      CUSTOMER = CUSTOMER.LIST<I>
      READ ITEM.CM FROM FILE.CM,CUSTOMER ELSE ITEM.CM=''
      *
      sales.rep = item.cm<cm$sales.rep>
      read item.user from file.user,sales.rep else
         item.user='not on user master file USER'
      end
      sales.rep.name=item.user<user$short.name>
      *
      row = sales.rep:TB
      row := sales.rep.name:TB
      row := CUSTOMER:TB
      row := item.cm<cm$name>:TB
      row := item.cm<cm$city>:TB
      row := item.cm<cm$state>:TB
      row := item.cm<cm$phone>
      *
      FOR YR.CNTR = 1 TO LAST.YR
         row := TB : OCONV(SALES.LIST<I,YR.CNTR>,'MR2,$')
      NEXT YR.CNTR
      *
      xls<-1> = row
   NEXT I
   *
   RETURN
   *
   99000 * E
   *
   *
   CALL SWAP(TB,VM,XLS)
   DT = OCONV(DATE(),'D4-')
   DATE.TIME = FIELD(DT,'-',3): FIELD(DT,'-',1): FIELD(DT,'-',2): '_' : TIME()
   PATHNAME = ''
   FILENAME = 'SalesReport_BySalesRep_':DATE.TIME:'.'
   HEADINGS = ''
   *
   CALL EXPORT.TO.EXCEL(PATHNAME, FILENAME, HEADINGS, XLS, STATUS)
   IF STATUS = '' ELSE
      CRT 'Transfer failed - ':STATUS
      CRT 'Press any key to continue: ':
      INPUT X,1
   END
